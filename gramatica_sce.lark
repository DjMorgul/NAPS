// NAPS: The New Age PAW-like System - Herramientas para sistemas PAW-like
//
// Gramática para código fuente SCE multi-sistema
// Copyright (C) 2023-2024 José Manuel Ferrer Ortiz

start: _wsnl? (ctl _wsnl)? (tok _wsnl)? voc _wsnl stx _wsnl mtx _wsnl otx _wsnl ltx _wsnl con _wsnl obj _wsnl pros _wsnlc

COMMENT: ";" /[^\n]/*
SYMBOL: /[A-Z_a-z][0-9A-Z_a-z]*/
expression: (SYMBOL | UINT) (_WS? OPERATOR _WS? (SYMBOL | UINT))*
OPERATOR: "+" | "-"
_WS: (" " | "\t")+            // Espacio en blanco
_wsc: _WS? COMMENT?           // Posible espacio en blanco y/o comentario
_wsnl: (_WS? COMMENT? "\n")+  // Posible espacio en blanco y/o comentarios, y después nueva línea
_wsnlc: _wsnl? _wsc           // Posible espacio en blanco y/o comentarios
INT: /[+-]?[0-9]+/
UINT: /[0-9]+/
VOCWORD: /[^ _;\n\t\/]/+
word: VOCWORD | "_"

ctl: _CTL (_wsnl /[A-Z]/)? _wsnl "_"  // (_wsnl /[A-Z]/) usado en Amstrad CPC para indicar la letra de unidad donde guardar la base de datos
_CTL: "/CTL"

tok: _TOK _wsnlc (_wsnl TOKEN)*
_TOK: "/TOK"
TOKEN: /[^ \n\t]/ /[^ \n\t]/+

voc: _VOC _wsnlc (_wsnl vocentry)*
_VOC: "/VOC"
vocentry: VOCWORD _WS UINT _WS voctype
voctype: ADJECTIVE | ADVERB | CONJUGATION | NOUN | PREPOSITION | PRONOUN | VERB
ADVERB: /adverb/i
ADJECTIVE: /adjective/i
CONJUGATION: /conjugation/i | /conjunction/i
NOUN: /noun/i
PREPOSITION: /preposition/i
PRONOUN: /pronoun/i
VERB: /verb/i

TEXTLINE: "\n" /[^\n\/][^\n]*/?
textentry: "/" (UINT | expression) _wsnlc TEXTLINE*

stx: _STX _wsnlc ("\n" textentry)*
_STX: "/STX"

mtx: _MTX _wsnlc ("\n" textentry)*
_MTX: "/MTX"

otx: _OTX _wsnlc ("\n" textentry)*
_OTX: "/OTX"

ltx: _LTX _wsnlc ("\n" textentry)*
_LTX: "/LTX"

con: _CON _wsnlc (_wsnl conentry)*
_CON: "/CON"
conentry: "/" UINT _wsnlc (_wsnl conitem)*
conitem: word _WS UINT

obj: _OBJ _wsnlc (_wsnl objentry)*
_OBJ: "/OBJ"
objentry: "/" UINT _WS objinitial _WS OBJWEIGHT _WS attr _WS attr (_WS attr)* _WS word _WS word
objinitial: INT | CARRIED | WORN | "_"
OBJWEIGHT: INT
attr: Y | "_"
Y: "Y"

CARRIED: "CARRIED"
HERE: "HERE"
WORN: "WORN"

pro: _PRO _WS UINT _wsnlc (_wsnl procentry)*
pros: pro (_wsnl pro)*
_PRO: "/PRO"
procentry: word _WS word (_WS condact)? _wsnlc (_wsnl _WS condact)*
condact: condactname param*
condactname: /[A-Z][A-Z0-9]*/
INDIRECTION: "[" UINT "]"
param: _WS (CARRIED | HERE | WORN | INT | expression | INDIRECTION | VOCWORD | "_")
